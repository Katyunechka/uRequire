pathRelative = require('../paths/pathRelative')
_ = require 'lodash'
#
#  A 'simple' template for a UMD module. Based on https://github.com/umdjs/umd/blob/master/returnExportsGlobal.js
#
#  @param o {Object} with
#   {
#     modulePath: where the module is, within bundle
#     moduleName: the moduleName, if it exists.
#     moduleType: type of the original module : 'node' or 'AMD'
#     type: 'define' or 'require': NOT USED
#     arrayDependencies: Array of deps, as delcared in AMD, filerelative (eg '../PersonView' for 'views/PersonView') + all `require('dep')`
#     nodeDependencies: Array for file-relative dependencies, as required by node (eg '../PersonView')
#     parameters: Array of parameter names, as declared on the original AMD.
#     rootExport: the name of the root variable to export on the browser side (or false/absent)
#     factoryBody: The actual code that returns our module (define) or just runs some code having dependencies resolved (require).
#     webRootMap: path of where to map '/' when running on node, relative to bundleRoot (starting with '.'), absolute OS path otherwise.
#  }
#
# @todo: recognise define [], -> or require [], -> and adjust both node & browser UMD accordingly
# @todo: make unit tests

class ModuleGeneratorTemplates
  constructor: (@o)->
    @header = "// Generated by uRequire v#{@o.version}"

    @moduleNamePrint = if o.moduleName then "'#{o.moduleName}', " else ""

    @parametersPrint = """
      require#{if (o.moduleType is 'node') then ', exports, module' else ''}#{
      (", #{par}" for par in o.parameters).join ''}
    """

    @arrayDependenciesPrint = """
      #{if _(o.arrayDependencies).isEmpty()
          "" #keep empty [] not existent, enabling requirejs scan
        else
          if o.moduleType is 'node'
            "['require', 'module', 'exports'"
          else
            "['require'"}#{(", '#{dep}'" for dep in o.arrayDependencies).join('')} #{
          if _(o.arrayDependencies).isEmpty() then "" else "], "
      }
    """

    @factoryBodyBracedPrint = """{
      var isWeb = (typeof define === 'function' && define.amd);
      var isNode = !isWeb;
      // uRequire: start body of original #{o.moduleType} module
      #{o.factoryBody}
      // uRequire: end body of original #{o.moduleType} module #{
      if (o.moduleType is 'node') then '\nreturn module.exports;' else ''}
    }
    """

  UMD: ->"""
    #{@header}
    (function (root, factory) {
      if (typeof exports === 'object') {
        var nr = new (require('urequire').NodeRequirer) ('#{@o.modulePath}', __dirname, '#{@o.webRootMap}');
        module.exports = factory(nr.require#{
          if (@o.moduleType is 'node') then ', exports, module' else ''}#{
          (", nr.require('#{nDep}')" for nDep in @o.nodeDependencies).join('')});
      } else if (typeof define === 'function' && define.amd) {
          define(#{@moduleNamePrint}#{@arrayDependenciesPrint}#{
            if @o.rootExport # Adds browser/root globals if needed
              "function (#{@parametersPrint}) { \n" +
              "          return (root.#{@o.rootExport} = factory(#{@parametersPrint}));\n" +
              "        });"
            else
              'factory);'
          }
      }
    }) (this, function (#{@parametersPrint}) #{@factoryBodyBracedPrint});
  """

  AMD: ->
    @header + '\n' +
    if not @o.rootExport # 'standard' AMD format
      """
      define(#{@moduleNamePrint}#{@arrayDependenciesPrint}
        function (#{@parametersPrint}) #{
          @factoryBodyBracedPrint}
      );
      """
    else # ammend to export window = @o.rootExport
      """
      define(#{@moduleNamePrint}#{@arrayDependenciesPrint}
        function (#{@parametersPrint}) {
          return (window.#{@o.rootExport} = (
            function (#{@parametersPrint}) #{
              @factoryBodyBracedPrint
            } (#{@parametersPrint}))
          );
        }
      );
      """

module.exports = ModuleGeneratorTemplates